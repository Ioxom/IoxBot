plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

mainClassName = 'ca.ioxom.ioxbot.Main'
group 'ca.ioxom'
version '0.2.2'
def jdaVersion = '4.2.0_227'
def jacksonVersion = '2.12.1'

repositories {
    jcenter()
}

dependencies {
    //json parsing library
    implementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion)
    //integration with discord
    implementation(group: 'net.dv8tion', name: 'JDA', version: jdaVersion)
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.encoding = 'UTF-8'
}

task('addVersion') {
    doLast {
        //append the version to a properties file so we can access it from java code
        file("src/main/resources/ioxbot.properties").write(
                "# ioxbot's version. this is stored here to allow project-wide version syncing\n" +
                "version=$version"
        )
    }
}

task uberJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName, 'Manifest-Version': 1.0
    }

    from sourceSets.main.output

    dependsOn(addVersion)
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}